apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

//./gradlew clean ./gradlew build ./gradlew bintrayUpload


group = project.GROUP_ID
version = project.VERSION

def getUsername() {
    return hasProperty('bintrayUser') ? bintrayUser : ""
}

def getPassword() {
    return hasProperty('bintrayApiKey') ? bintrayApiKey : ""
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://androiddoc.qiniudn.com/reference/"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Production(MavenPublication) {
            groupId project.GROUP_ID
            artifactId project.ARTIFACT_ID
            version project.VERSION

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    def bintrayUser = getUsername()
    def bintrayApiKey = getPassword()

    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = "maven"
        name = project.NAME
        licenses = ["Apache-2.0"]
        publish = true
        description = "Android CGLib library for dynamic proxy class"
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/xuuhaoo/Cglib-Android'
        dryRun = false
        version {
            name = project.VERSION
            desc = "Android CGLib library for dynamic proxy class"
            released = new Date()
            vcsTag = project.VERSION
        }
    }
}
